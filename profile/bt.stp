#! /usr/bin/env stap
# based on nginx backtrace by Copyright (C) Yichun Zhang (agentzh)

global bts, target_pid, target_name

probe timer.profile {
    if ((is_target()) && !user_mode()) {
        bts[backtrace(), tid()] <<< 1
    }
}

function is_target() {
    if (target_name != "") {
    	return target_name == execname()
    }
    return target_pid == pid()
}

probe end {
    nstacks = 0
    foreach ([a, b] in bts limit 1) {
        nstacks++
    }

    if (nstacks == 0) {
        warn("No backtraces found. Quitting now...\\n")
    } else {
	#remove this 1024 limit ?
        foreach ([bt, _tid] in bts- limit 1024) {
            print_stack(bt)
            printf("\t%d pid=%d tid=%d\n", @count(bts[bt, _tid]), target(), _tid)
        }
    }
}

probe begin {
    target_pid = 0
    target_name = ""

    %( $# == 1 || $# > 2 %?
        log("Wrong number of arguments, use none, 'pid nr' or 'name proc'")
	exit()
    %)

    %( $# == 2 %?
	if(@1 == "pid") {
	    target_pid = strtol(@2, 10)
            printf("#target_pid=%d\n", target_pid)
	}
	if(@1 == "name") {
	    target_name = @2
            printf("#target_name=%s\n", target_name)
	}
    %)
}
